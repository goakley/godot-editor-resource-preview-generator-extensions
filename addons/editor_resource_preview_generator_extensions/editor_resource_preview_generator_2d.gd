@abstract
@tool
@icon("icon.svg")
class_name EditorResourcePreviewGenerator2D
extends EditorResourcePreviewGenerator
## Custom generator of previews using canvas drawing methods.
##
## Custom code to generate previews using [CanvasItem]-style drawing functions.
## Subclasses must implement [method _generate_2d].
##
## [codeblock lang=gdscript]
## # my_resource.gd
##
## class_name MyResource
## extends Resource
##
## @export var color: Color
##
## # my_resource_generator.gd
##
## extends EditorResourcePreviewGenerator2D
##
## func _handles(type: String) -> bool:
##     return type == "Resource"
##
## func _generate_2d(resource: Resource, canvas: EditorResourcePreviewGeneratorCanvas, metadata: Dictionary) -> bool:
##     if resource is not MyResource:
##         return false
##     canvas.draw_rect(Rect2(Vector2.ZERO, canvas.get_size()), resource.color)
##     return true
## [/codeblock]


func _generate(resource: Resource, size: Vector2i, metadata: Dictionary) -> Texture2D:
	var canvas := EditorResourcePreviewGeneratorCanvas.new(size)
	var ok := _generate_2d(resource, canvas, metadata)
	if !ok:
		return null
	return canvas._draw()


## If this function returns [code]true[/code],
## the generator will call [method _generate]
## for small previews as well.
##
## By default, it returns [code]false[/code].
func _can_generate_small_preview() -> bool:
	return false


## If this function returns [code]true[/code],
## the generator will automatically generate the small previews
## from the normal preview texture generated by the method
## [method _generate_2d].
##
## By default, it returns [code]false[/code].
func _generate_small_preview_automatically() -> bool:
	return false


## Generate a preview from a given resource using the given canvas helper.
## This must always be implemented.
##
## Returning `false` is an OK way to fail and let another generator take over.
##
## Care must be taken because this function is always called from a thread
## (not the main thread).
##
## The `metadata` dictionary can be modified to store file-specific metadata
## that can be used in EditorResourceTooltipPlugin._make_tooltip_for_path()
## (like image size, sample length etc.).
@abstract
func _generate_2d(resource: Resource, canvas: EditorResourcePreviewGeneratorCanvas, metadata: Dictionary) -> bool

## Returns [code]true[/code] if your generator supports the resource
## of type [code]type[/code].
@abstract
func _handles(type: String) -> bool
